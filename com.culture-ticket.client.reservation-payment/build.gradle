plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.0'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.epages.restdocs-api-spec' version "0.19.2"
}

group = 'com.culture-ticket.client'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2024.0.0")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // eureka client
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    // openfeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    // querydsl for spring boot 3.x
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    // kafka
    implementation 'org.springframework.kafka:spring-kafka'

	implementation 'org.springframework.boot:spring-boot-starter-web'
	// validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	// JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	// PostgreSQL
	runtimeOnly 'org.postgresql:postgresql'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	// restdocs & openapi
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.2'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// 이하 Swagger 문서 생성 설정
compileJava {
	dependsOn 'clean'
}

postman {
	baseUrl = 'http://localhost:19091' // gateway에 호출이 들어오기 때문에 gateway의 포트를 설정해 주어야 함
	title = 'RESERVATION-PAYMENT API' // 각 서비스 제목 적기
	version = '1.0.0'
}

openapi3 {
	servers = [
			{
				url = 'http://localhost:19091' // 마찬가지로
			}
	]
	title = 'RESERVATION-PAYMENT API' // 각 서비스 제목 적기
	description = ''
	version = '1.0.0'
	format = 'json'
}

tasks.register('setDocs') {
	dependsOn 'postman'
	dependsOn 'openapi3'
	doLast {
		copy {
			from "build/api-spec"
			include "*.json"
			include "*.yaml"
			into "build/resources/main/static/springdoc"
			rename { String fileName ->
				if (fileName.endsWith('.json')) {
					return fileName.replace('.json', '-reservation-payment-service.json') // 각 서비스 제목 적기
				} else if (fileName.endsWith('.yaml')) {
					return fileName.replace('.yaml', '-reservation-payment-service.yml') // 각 서비스 제목 적기
				}
				return fileName
			}
		}
	}
}

bootRun {
	dependsOn 'setDocs'
}

bootJar {
	dependsOn 'setDocs'
}